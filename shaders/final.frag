#version 330 core
out vec4 FragColor;
in vec2 vUv;

uniform sampler2D uSpaceTexture;
uniform sampler2D uDistortionTexture;
uniform vec2 uBlackHolePosition;
uniform float uRGBShiftRadius;

const float PI = 3.1415926538;

vec3 getRGBShiftedColor(sampler2D _texture, vec2 _uv, float _radius) {
    vec3 angle = vec3(PI * 2.0 / 3.0, PI * 4.0 / 3.0, 0.0);
    vec3 color;
    color.r = texture(_texture, _uv + vec2(sin(angle.r) * _radius, cos(angle.r) * _radius)).r;
    color.g = texture(_texture, _uv + vec2(sin(angle.g) * _radius, cos(angle.g) * _radius)).g;
    color.b = texture(_texture, _uv + vec2(sin(angle.b) * _radius, cos(angle.b) * _radius)).b;
    return color;
}

void main() {
    vec4 distortionColor = texture(uDistortionTexture, vUv);
    float distortionIntensity = distortionColor.r;
    vec2 towardCenter = vUv - uBlackHolePosition;
    towardCenter *= -distortionIntensity * 2.0;

    vec2 distortedUv = vUv + towardCenter;
    vec3 outColor = getRGBShiftedColor(uSpaceTexture, distortedUv, uRGBShiftRadius);

    FragColor = vec4(outColor, 1.0);
}
